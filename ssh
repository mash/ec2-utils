#!/usr/bin/env perl
use strict;
use warnings;
use Config::Pit;
use Net::Amazon::EC2;
use Getopt::Long;
use Term::Prompt;

my ($result, $pit, $region);
$region = 'us-west-1';
$result = GetOptions ( "pit|p=s"    => \$pit,
                       "region|r:s" => \$region)
    or die usage();

my $group_id = shift @ARGV
    or die usage();

$pit
    or die usage();

my $config = pit_get( $pit, require => {
    AWSAccessKeyId  => 'access key id',
    SecretAccessKey => 'secret access key',
});

my $ec2 = Net::Amazon::EC2->new(
    AWSAccessKeyId  => $config->{AWSAccessKeyId},
    SecretAccessKey => $config->{SecretAccessKey},
    region          => $region,
);

my $instances = $ec2->describe_instances;
my @running_instances = grep { $_->instances_set->[0]->instance_state->name eq 'running' } @{ $instances };
print "total running: " . scalar(@running_instances) . "\n";

my @group_instances   = grep { $_->group_set->[0]->group_id eq $group_id } @running_instances;
print "running [$group_id] instances: " . scalar @group_instances . "\n";

die "no $group_id servers found\n" if ( scalar @group_instances == 0 );

my @results = (scalar @group_instances == 1) ? (0) : prompt(
    'm',
    +{
        prompt                 => 'which server to connect?',
        title                  => 'choose target server',
        items                  => [ map { $_->instances_set->[0]->dns_name; } @group_instances ],
        cols                   => 1,
        accept_empty_selection => 0,
    },
    'input single number',
    1
);

my $publicdns_name    = $group_instances[ $results[0] ]->instances_set->[0]->dns_name;

print "connecting to $group_id server: root\@$publicdns_name\n";
my $command = "ssh root\@${publicdns_name} @ARGV";
exec $command;

sub usage {
    "usage: $0 -p <pit_key> -r 'us-west-1' <group_id> -- -i path/to/keypair.pem";
}
